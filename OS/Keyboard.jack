// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static Array inputBuf;

    /** Initializes the keyboard. */
    function void init() {
        let inputBuf = Array.new(128);
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        do Output.printChar(0);
        let c = 0;
        while (c = 0) {
            let c = Keyboard.keyPressed();
        }
        while (Keyboard.keyPressed() > 0) {}
        do Output.backSpace();
        do Output.printChar(c);
        return c;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var int charNum, inputLen;
        var char c;
        var String input;
        do Output.printString(message);
        let charNum = 0;
        while (true) {
            let c = Keyboard.readChar();
            if (c = 128) {
                let inputLen = charNum;
                let input = String.new(inputLen);
                let charNum = 0;
                while (charNum < inputLen) {
                    do input.appendChar(inputBuf[charNum]);
                    let charNum = charNum + 1;
                }
                return input;
            }
            if (c = 129) {
                let charNum = charNum - 1;
            } else {
                let inputBuf[charNum] = c;
                let charNum = charNum + 1;
            }
        }
        return 0;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String input;
        let input = Keyboard.readLine(message);
        return input.intValue();
    }
}
